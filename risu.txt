/* licensed under CC0 1.0 */

OUTPUT_FORMAT(elf32-littleriscv)
ENTRY(_start)

MEMORY {
	FLASH (xr): ORIGIN = 0x08000000, LENGTH = 128K
	SRAM (xw): ORIGIN = 0x20000000, LENGTH = 32K
}

PHDRS {
	text PT_LOAD FLAGS (6 /* SHF_ALLOC | SHF_EXECINSTR */);
	data PT_LOAD FLAGS (7 /* SHF_WRITE | SHF_ALLOC | SHF_EXECINSTR */);
}

SECTIONS {
	.startup : {
		. = ALIGN(4);
		KEEP(*(.startup));
		. = ALIGN(4);
	} > FLASH :text

	.text : {
		. = ALIGN(4);
		*(.text)
		*(.text*)
		. = ALIGN(4);
	} > FLASH :text

	.rodata : {
		. = ALIGN(4);
		*(.rodata)
		*(.rodata*)
		. = ALIGN(4);
	} > FLASH :text

	.data : {
		. = ALIGN(4);
		_init_data_dest_start = .;
		*(.data)
		*(.data*)
		. = ALIGN(4);
		*(.sdata)
		*(.sdata*)
		. = ALIGN(4);
		_init_data_dest_end = .;
	} > SRAM AT > FLASH :data
	_init_data_src_start = . + LOADADDR(.data) - ADDR(.data);

	.bss : {
		. = ALIGN(4);
		_bss_start = .;
		*(.bss)
		*(.bss*)
		. = ALIGN(4);
		*(.sbss)
		*(.sbss*)
		. = ALIGN(4);
		_bss_end = .;
	} > SRAM :data
}
